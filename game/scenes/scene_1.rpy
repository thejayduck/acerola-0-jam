label scene_1:
  cassian "mmm- ahhh…"

  "I woke up from my long nap on the uncomfortable sofa and stretched my entire body."
  "How long have I been sleeping for?"
  "My entire body feels sluggish…"
  "I guess sleeping in the middle of the day wasn't a good idea after all."
  "I then open my eyes and let them adjust to the brightly lit room."
  "I look at the two people sitting across me and blankly stare at them."

  play music "audio/bgm/casual.mp3" fadein 5.0

  scene living_room with fade

  cassian "..."

  show franz tired at right
  with moveinright

  "The first one I look at is Franz, he doesn't react to my stare in any way."

  "We live under the same roof, not because we wanted to, but because at the beginning of the first semester, our university, conducted a random poll, and we got stuck with each other ever since."
  "Not that I am complaining or anything… he has been a good friend since then."  

  "I guess he is just as tired as I am…"

  hide franz with moveoutright

  show maike with dissolve
  pause 1.0
  show maike tired with dissolve

  "I then focus on Maike, and she immediately returns my stare with a colder one."

  maike "..."

  maike "What is it?"

  cassian "When'd you arrive here?"

  "Maike is… not sure actually, she has been following me for a long time."
  "Her presence didn't bother me so I never had to kick her out of the apartment."
  "Unlike Franz and I, she lives with her parents."
  "I believe it's around a 30-minute train ride from where the university is, it must be pretty nice to eat homemade food every day, and not have to think about the upcoming rent."

  maike "You were the one to suggest that we all gather and finish our work."
  cassian "Oh right that… I will do it another day."

  "We still have a few days until the deadline, what is she so worked up about on a whatever-thousand-word essay."

  cassian "W-What are you staring at…"

  maike "You keep saying that but end up paraphrasing my entire essay at the end, I would be shocked if you managed your time well for once."
  maike "I might consider not showing you my whatever-thousand-word essay this time."

  "Urk!" with hpunch

  cassian "Huh?! D-Did you read my min- are you an esper perchance?!"

  show maike crossed normal with dissolve

  maike "Like hell I am! The way you think is so straightforward an infant can memorize your thought patterns in a few days."

  cassian "Heh- Fair point, I'll give you that. But even without your help, I'll manage to finish it. After all- {nw}"

  show franz surprised at right with moveinright

  show maike:
    xzoom -1.0
  with dissolve

  franz "Wah! I just remembered!" with hpunch

  cassian "You don't have to scream like that you know!"

  maike "..."

  show franz sad

  franz "Sorry~ I won't do it again~"

  cassian "So, what did you remember?"

  show maike confused

  maike "Probably the fact that he doesn't have time to finish the essay on time."

  "I don't think he is the kind of person to worry about things like that."
  "I kept that thought to myself."

  show franz hip normal with dissolve

  franz "Heheh… There's apparently a new fortune teller in the city, quite close too!"

  cassian "There's another one? Is the job that profitable that people make a lot of money out of it?"

  maike "..."

  show maike tired

  cassian "Now that I mention that… We got scammed far too many times thanks to those 'new fortune tellers', haven't we."

  maike "Indeed."

  "I don't want to get legally robbed out of my own money again."
  "We've been to an insane amount of them thanks to Franz, and unsurprisingly all of them turned out to be fake."
  "Of course, without proof that we paid for the place, we weren't allowed a refund, even after they got caught."
  "I am starting to run short on my savings, I don't want to start working again to get by."

  cassian "We are not going. {nw}"

  franz "What about you Maike, do you want to join us?"

  cassian "Hey hey I said I am not coming! {nw}"

  show maike normal hip with dissolve

  maike "Well, I did end up writing three thousand words in the last 45 minutes, a little break wouldn't hurt."

  show franz happy

  franz "Nice! I'll get ready then!"

  hide franz with moveoutleft

  "......"

  "Guh… I know they ignore me sometimes, but this one hurt. A lot."

  scene black with fade

  "Although forced, I reluctantly entered my room to change my clothes, and get my phone."
  
  play audio "audio/sfx/generic/door_close_fast.mp3"

  scene city_empty
  show franz:
    xpos .1
    walk
  show maike:
    xpos .5
    walk(.8)
  with fade

  cassian "Huff… Huff…"

  "We've been following behind Franz for the past 30 minutes or so, I am hesitant to look and confirm the time on my phone."
  "I look at Maike walking behind me, she seems fine for someone who prefers to spend hours on end working on assignments."

  cassian "D-Didn't you say this place was close…"

  show franz happy

  franz "Hmm~ But it is close. I am just taking a detour to see if there's an interesting place."
  
  cassian "...{nw}"

  show maike tired crossed:
    xpos .5
  with dissolve

  maike "..."

  "I pick up my walking pace, swiftly closing the distance between Franz and me."
  "I've been itching to do this since we left the apartment."

  # play audio "audio/sfx/generic/walk_towards.mp3"

  play audio "audio/sfx/generic/punch.mp3"
  scene franz_punch with hpunch

  with vpunch

  franz "Owwww~!"

  "Maike and I planted our fists into Franz's skull."
  "Tough. Was the first thing that came to my mind as soon as I realized that he screamed while laughing."
  "Is this guy for real?"

  cassian "Will you stop exploring every corner of the city, are you trying to get us lost?"

  maike "Don't make me waste more of my time by getting us lost."

  franz "But aren't you guys having fun as well?"

  play audio "audio/sfx/generic/punch.mp3"
  with hpunch
  with vpunch

  franz "OK OK. I won't do it again… for now…"

  play audio "audio/sfx/generic/punch.mp3"
  with hpunch
  with vpunch

  franz "What was that for?"

  maike "A reward for your understanding."

  cassian "Exactly…"

  scene black with fade

  "After confronting Franz on wasting our time, we followed him directly to the Fortune Teller that he had been excited about." 

  scene fortune_teller
  show franz happy hip at left
  show maike at right
  with fade

  franz "Ta-dah!"

  franz "This is the place! Mystic's Fate Deceiving Prophecies."

  cassian "Maike… What do you think about this place?"

  show maike worried with dissolve

  maike "T-This might be the worst one we are about to experience, brace yourselves."

  "Even the emotionless Maike thinks this is a bad idea, huh… It's rare to see her this nervous, maybe  I should listen to her this time."

  show franz annoyed

  franz "What are you two talking about?" with hpunch

  franz "Can't you see how undecorated this place is, there aren't any big signs directing to this place, and is only known by those who care about their fortunes."
  
  franz "The fortune teller must be a big deal to do that."

  franz "Even I can acknowledge that the previous locations were sketchy… but this one is special."

  "This guy is hopeless. This isn't the first time we came across a fortune teller shop that looked different compared to the competition, yet he keeps praising each one of them."

  "..."

  show fortune_teller:
    glitch("fortune_teller", chroma=True, offset=10)
    pause 0.2
    "fortune_teller"

  "Looking at it again, this one does feel… weird."

  show franz happy

  franz "Ooh, the door opened itself when it detected our presence, mysterious."

  cassian "Dude, they are just motion detectors."

  franz "I am going in."

  play audio "audio/sfx/generic/running.mp3"

  hide franz with dissolve
  show maike normal

  "The inside of Mystic's store was pitch black, so dark that even with immense focus I couldn't make out whether the room was furnished, or simply an empty void that sucked up everything in its proximity."

  cassian "Hey! Franz! How is it inside?"

  franz "..."

  cassian "Franz?" with hpunch

  franz "Ah sorry, I was trying to find the light switch."

  cassian "Hold on, I'll turn on my flashlight and find a light switch."

  play audio "audio/sfx/generic/phone_tap.mp3"

  "I get my phone out of my front pocket and tap the flashlight button in the quick menu."

  cassian "... Are you seeing this Maike?"

  maike "..."

  "She nods slightly at my question."

  "Anyways, why can't I… see anything in this place?"
  "Is it one of those paints that absorb the majority of the light?"

  cassian "Franz where are you? I can't see you from the entrance."

  franz "I might be a little lost heheh~ I can't see the door anymore."

  show maike tired

  "That damned kid in an adult form, I am going to leave you here all by yourself."

  "Is what I would like to say and do, but I am not in the mood to listen to my intrusive thoughts."

  cassian "Maike, I am going in, you stay here just in case something happens, okay?"

  maike "...{nw}"

  show maike normal crossed with dissolve

  maike "I will come with you."

  cassian "Huh? But wouldn't it be bad if all of us got lost?"

  maike "If I don't, I won't be able to strike Franz's head once again after we find him."

  cassian "So you want to team up on him once again?"

  show maike smirk

  maike "Exactly."

  "Hmph, this girl sure is on my side when it comes to punishing Franz."

  # play audio "audio/sfx/generic/walking.mp3"

  hide maike with dissolve

  scene fortune_teller:
    xalign 0.75
    yalign 0.55 
    ease 3.5 zoom 1.5
  pause 2.0

  scene black with fade

  "Maike, together with me enters the void."

  stop music fadeout 0.5
  pause 0.5
  play music "/audio/bgm/warp.mp3" fadein 0.5

  "Wow, this place is cold. Like… abnormally cold, I don't think a consumer AC can achieve such temperatures."

  cassian "Maike, how are you holding over there, isn't this too cold for you?"

  cassian "..."

  "I turn my head towards Maike, but she is missing, or is it the other way around, I am not sure."
  "But damn this place is dark, is it like a maze that eventually leads to the fortune teller or something?"

  cassian "..."

  scene warp_1 with fade

  stop music fadeout 4.0  
  play music "audio/bgm/something_is_wrong.mp3" fadein 2.0 volume .8

  "Is this some kind of simulation room?"

  "Maybe the two were eager to come here to surprise me."

  "But for what?"

  cassian "..."

  show warp_1:
    parallel:
      function WaveShader(amp=0.2, direction="vertical", double="horizontal")
  with dissolve

  "I can't speak… I try to look around but the stars emitting from the projector start to multiply…"
  "I try to stay awake, but it feels like my soul doesn't want to…"
  "I- I don't even want to think anymore…"

  "The world around me... starts to warp."
  $ active_background = "warp_1"
  $ confirm_input("scene_2", "725DD2")